plugins {
    id "com.gradle.plugin-publish" version "0.9.7"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile 'org.ajoberstar:grgit:1.6.0'
  compile('org.hidetake:groovy-ssh:2.7.0') {
      exclude module: 'groovy-all'
  }
}

repositories {
    jcenter()
}

group = "jaci.openrio.cpp.gradle"
archivesBaseName = "GradleRIO-C"
version = "2017.3.3"

pluginBundle {
    website = 'https://github.com/Open-RIO/GradleRIO-C'
    vcsUrl = 'https://github.com/Open-RIO/GradleRIO-C'
    description = 'Managing FRC C++ projects, the Gradle way'
    tags = ['frc', 'wpilib', 'toast', 'openrio', 'gradlerio']

    plugins {
        wpilibVersionPlugin {
            id = "${project.group}.${project.archivesBaseName}"
            displayName = 'GradleRIO C'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact jar
        }
    }
}

task deleteQuickstart() {
    doLast {
        def f = new File("Quickstart.zip")
        if (f.exists()) f.delete()
    }
}

task zipQuickstart(type: Zip) {
    dependsOn deleteQuickstart
    from('quickstart')
    include '**/*.cpp', '**/*.hpp', 'gradle/**/*', 'build.gradle', 'settings.gradle', 'gradlew', 'gradlew.bat', '.gitignore'
    exclude '.vscode', '.gradle'
    destinationDir projectDir
    archiveName 'Quickstart.zip'
}

build.dependsOn zipQuickstart

// Release both plugin and build other projects
task fullRelease() {
    dependsOn build
    dependsOn publishPlugins
    dependsOn zipQuickstart
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
